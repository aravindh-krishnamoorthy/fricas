)set break resume
)expose UnittestCount UnittestAux Unittest

testsuite "bugs2024"

testcase "name clash"

-- Bug occured during initialization of RSIMP, so test must be
-- before any other use of RSIMP

a : Integer := 1
testTrue("rsimp(1)$RootSimplification; true")

testcase "simplifyExp"

testEquals("simplifyExp(2*2^a)", "2^(a+1)")
testEquals("simplifyExp(a*a^b)", "a^(b+1)")
testEquals("simplifyExp((a^2)^b*a^2)", "(a^2)^(b+1)")
testEquals("simplifyExp((a^b)^c*a^b)", "(a^b)^(c+1)")
testEquals("simplifyExp((a^b)^c*a^(b-d)*a^d)", "(a^b)^(c+1)")

testcase "error in has test"

-- Used to produce system error: "The variable % is unbound."
testTrue("not(SemiRng has AbelianGroup)")

testcase "partial fractions"

p1 := 1 + partialFraction(-3, 7)
p2 := partialFraction(4, 7)
testTrue("(p1 = p2)@Boolean")

testEquals("numberOfFractionalTerms(partialFraction(7, 7))", "0")
testEquals("numberOfFractionalTerms(partialFraction(-7, 7))", "0")

testcase "radical solve"

p1 := 64*z^4 + 64*z^3 + 32*z^2 - 8*z + 1
rl1 := radicalSolve(p1)
testEquals("eval(p1, first(rl1))", "0")
rr1 := [rhs(s1) for s1 in rl1]
testEquals("reduce(max, [height(k1) for k1 in kernels(rr1)])", "1")

p2 := x^4 - 2729960418308000*x^3 - 395258439243352250000*x^2 - _
        55499520947716391500000000*x - 345363656226658026765625000000

rl2 := radicalSolve(p2);
testEquals("eval(p2, first(rl2))", "0")
rr2 := [rhs(s1) for s1 in rl2];
testEquals("reduce(max, [height(k1) for k1 in kernels(rr2)])", "2")

statistics()
